apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    namespace "com.simplemobiletools.flashlight"
    compileSdk 34

    defaultConfig {
        applicationId "com.simplemobiletools.flashlight"
        minSdk 23
        targetSdk 34
        versionCode 64
        versionName "5.9.2"
        setProperty("archivesBaseName", "flashlight")
    }

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    buildTypes {
        debug {
	        applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    flavorDimensions = ["variants"]
    productFlavors {
        core {}
        fdroid {}
        prepaid {}
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation 'com.github.SimpleMobileTools:Simple-Commons:7c1e5b5777'
    implementation 'org.greenrobot:eventbus:3.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}
